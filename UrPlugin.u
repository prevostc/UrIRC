
requireFile("UrIRC.u");


/*
 * This class provide a base structure of a class accessing the UrBot data. 
 * Please ensure that your plugin does nothing before the start call and after the stop call.
 * This feature avoid complete script reload (and bot deconnection) while reloading a plugin.
 */
class UrIRC.UrPlugin {
	// the UrBot class 
	var bot = nil;

	/*
	 * Tag holding watchdogs and background jobs
	 * Used by start and stop
	 */
	var _jobs = nil;

	/* 
	 * This init function call configure with a List of arguments equal to the init arguments exept the first
	 * This is a hack, I will change this behaviour (or not) if urbiscript support the "super" keyword.
	 *
	 * Example : 
	 * class UrIRC.UrExample : UrIRC.UrPlugin {
	 *	function configure(params) {
	 *		for (var param : params) echo(param);
	 *	}
	 * }
	 * var bot = UrIRC.UrBot.new(...);
	 * // here configure will have this list as argument [42, "Hello world", [1, 2]]
	 * UrIRC.UrExample.new(bot, 42, "Hello world", [1, 2]);
	 * 
	 *
	 * @param UrBot bot : the UrBot class
	 */
	function init {
		var args = call.evalArgs;
		bot = args[0];
		_jobs = Tag.new;
		configure(args.tail);
	};

	/* 
	 * Default configure slot does nothing
	 * You certainly want to override this slot
	 * configure should not start jobs or routines but just load and save what it needs.
	 */
	var configure = function(params){};

	/*
	 * Default start slot does nothing
	 * You certainly want to override this slot
	 * This slot should actually do some work and declare your watchdogs inside the _jobs tag
	 */
	var start = function() {};
		
	/*
	 * Default stop slot just stop rinnung jobs
	 * This slot should actually stop everything that your plugin may be working on
	 * Feel free to override it, but keep the same semantic
	 * 
	 * Notice: 
	 * Don't forget that Tag.clone create a "child" Tag which is also stopped by his parent stop.
	 * With this property, you can create your own tags, childs of _jobs and keep this slot
	 *
	 * @return UrPlugin this : current object
	 */
	var stop = function() {
		_jobs.stop;
		this;
	};
};


